apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlinx-serialization'
apply plugin: 'org.jlleitschuh.gradle.ktlint'

android {
    def minSdk = 21
    def maxSdk = 29

    compileSdkVersion maxSdk

    defaultConfig {
        applicationId "com.dtuskenis.papajohnscodes"

        minSdkVersion minSdk
        targetSdkVersion maxSdk

        versionCode 1
        versionName "1.0"

        testInstrumentationRunner ".InstrumentationRunner"

        buildConfigField "int", "MIN_SDK", "$minSdk"
        buildConfigField "int", "MAX_SDK", "$maxSdk"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    sourceSets {
        test {
            java.srcDir 'src/testCommons/java'
            resources.srcDir 'src/testCommons/resources'
        }
        androidTest {
            java.srcDir 'src/testCommons/java'
            resources.srcDir 'src/testCommons/resources'
        }
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.4.1"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.4.0"
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:1.0.1"

    implementation "androidx.appcompat:appcompat:1.2.0"
    implementation "androidx.recyclerview:recyclerview:1.1.0"

    implementation "com.squareup.retrofit2:retrofit:2.9.0"
    implementation "com.jakewharton.retrofit:retrofit2-kotlinx-serialization-converter:0.8.0"

    def espressoVersion = '3.3.0'

    androidTestImplementation "androidx.test.espresso:espresso-core:$espressoVersion"
    androidTestImplementation "androidx.test.espresso:espresso-contrib:$espressoVersion"

    def androidTestLibrariesVersion = '1.3.0'

    androidTestImplementation "androidx.test:runner:$androidTestLibrariesVersion"
    androidTestImplementation "androidx.test:rules:$androidTestLibrariesVersion"

    testImplementation "junit:junit:4.13"
    testImplementation "org.robolectric:robolectric:4.4"
    testImplementation "androidx.test:core:$androidTestLibrariesVersion"
    testImplementation "androidx.test.espresso:espresso-core:$espressoVersion"
    testImplementation "androidx.test.espresso:espresso-contrib:$espressoVersion"
}

ktlint {
    verbose = true
    android = true
    // disabledRules = ["final-newline"]
}